mutable struct RelaxationExecutionStats{T, VT} <: AbstractExecutionStats
    options::MadNLP.AbstractOptions
    mpcc_options::MadNLP.AbstractOptions
    status::MadNLP.Status
    objective::T
    solution::VT
    constraints::VT
    multipliers::VT
    multipliers_L::VT
    multipliers_U::VT
    multipliers_x1::VT
    multipliers_x2::VT
    dual_feas::T
    primal_feas::T
    inf_pr_cc::T
    iter::Int
    counters::RelaxationCounters
end

function RelaxationExecutionStats(solver::RelaxationSolver)
    n, m = get_nvar(solver.rnlp), get_ncon(solver.rnlp)
    ncc = get_ncc(solver.mpcc)
    VT = typeof(get_x0(solver.rnlp))
    x = similar(VT, n)
    zl = similar(VT, n)
    zu = similar(VT, n)
    zx1 = similar(VT, ncc)
    zx2 = similar(VT, ncc)
    c = similar(VT, m)
    y = similar(VT, m)
    n = MadNLP.get_nvar(solver.ipm.nlp)
    m = get_ncon(solver.mpcc)
    ind_cc1 = solver.ind_cc1
    ind_cc2 = solver.ind_cc2

    return RelaxationExecutionStats(
        solver.ipm.opt,
        solver.opts,
        solver.ipm.status,
        MadNLP.unpack_obj(solver.ipm.cb, solver.ipm.obj_val),
        x,
        c,
        y,
        zl,
        zu,
        zx1,
        zx2,
        solver.ipm.inf_du,
        solver.ipm.inf_pr,
        solver.inf_pr_cc,
        0,
        solver.cnt,
    )
end

# TODO(@anton) this is a hack, fix it
function SolverCore.getStatus(result::RelaxationExecutionStats)
    return MadNLP.get_status_output(result.status, result.options)
end

function log_iter(
    logger::IterateLogger,
    solver::RelaxationSolver{T, VT};
    magic=false,
) where {T, VT}
    if isnothing(logger.file)
        return nothing
    end
    ipm = solver.ipm
    ncc = get_ncc(solver.mpcc)

    k = ipm.cnt.k
    x0 = MadNLP.variable(ipm.x)[get_ind_x(solver.mpcc)]
    x1 = MadNLP.variable(ipm.x)[solver.ind_cc1]
    x2 = MadNLP.variable(ipm.x)[solver.ind_cc2]
    s = MadNLP.slack(ipm.x)[(end-ncc+1):end]

    z1 = MadNLP.variable(ipm.zl)[solver.ind_cc1]
    z2 = MadNLP.variable(ipm.zl)[solver.ind_cc2]
    zs = MadNLP.slack(ipm.zu)[(end-ncc+1):end]

    y = ipm.y

    p = ipm.p

    alpha_pr = ipm.alpha
    alpha_du = ipm.alpha_z

    ls = ipm.cnt.l

    obj = ipm.obj_val
    inf_pr = ipm.inf_pr
    inf_du = ipm.inf_du

    theta = MadNLP.get_theta(ipm.c)
    varphi = MadNLP.get_varphi(ipm.obj_val, ipm.x_lr, ipm.xl_r, ipm.xu_r, ipm.x_ur, ipm.mu)

    mu = ipm.mu
    sigma = get_relaxation(solver.rnlp)
    nu1 = solver.multipliers_cc1
    nu2 = solver.multipliers_cc2
    nu1_filt = solver.multipliers_cc1_filt
    nu2_filt = solver.multipliers_cc2_filt
    delta1 = solver.rnlp.δ1
    delta2 = solver.rnlp.δ2

    W = ipm.kkt.aug_com
    KKT_s = VT()#K = Array(Symmetric(W, :L)); eigvals(K)

    iter = RelaxationIterate(
        k,
        x0,
        x1,
        x2,
        s,
        z1,
        z2,
        zs,
        y,
        p,
        alpha_pr,
        alpha_du,
        ls,
        obj,
        inf_pr,
        inf_du,
        theta,
        varphi,
        mu,
        sigma,
        nu1,
        nu2,
        nu1_filt,
        nu2_filt,
        delta1,
        delta2,
        KKT_s,
        magic,
    )
    serialize(logger.file, iter)

    return nothing
end

function finalize(logger::IterateLogger)
    if isnothing(logger.file)
        return nothing
    end

    return close(logger.file)
end

function get_inf_pr_cc(solver::RelaxationSolver{T}) where {T}
    return @views(
        mapreduce(
            (a, la, b, lb) -> max((a-la)*(b-lb), la-a, lb-b),
            max,
            MadNLP.variable(solver.ipm.x)[solver.ind_cc1],
            get_lvar(solver.mpcc)[get_ind_cc1(solver.mpcc)],
            MadNLP.variable(solver.ipm.x)[solver.ind_cc2],
            get_lvar(solver.mpcc)[get_ind_cc2(solver.mpcc)];
            init=zero(T),
        )
    )
end

function MadNLP.print_iter(solver::RelaxationSolver; is_resto=false)
    ipm = solver.ipm
    obj_scale = ipm.cb.obj_scale[]
    mod(ipm.cnt.k, 10)==0 && MadNLP.@info(
        ipm.logger,
        @sprintf(
            "iter    objective    inf_pr  inf_rnlp   inf_du inf_comp lg(mu)  ||d||  lg(rg) alpha_du alpha_pr  ls lg(σ)   inf_cc"
        )
    )
    if is_resto
        RR = ipm.RR::RobustRestorer
        inf_du = RR.inf_du_R
        inf_pr = RR.inf_pr_R
        inf_compl = RR.inf_compl_R
        mu = log10(RR.mu_R)
    else
        inf_du = ipm.inf_du
        inf_pr = ipm.inf_pr
        inf_compl = ipm.inf_compl
        mu = log10(ipm.mu)
    end
    MadNLP.@info(
        ipm.logger,
        @sprintf(
            "%4i%s% 10.7e %6.2e %6.2e %6.2e %7.2e %5.1f %6.2e %s %6.2e %6.2e%s  %i %5.1f  %6.2e",
            ipm.cnt.k,
            is_resto ? "r" : " ",
            ipm.obj_val/obj_scale,
            inf_pr,
            MadNLP.get_inf_pr(ipm.c),
            inf_du,
            inf_compl,
            mu,
            ipm.cnt.k == 0 ? 0.0 : norm(MadNLP.primal(ipm.d), Inf),
            ipm.del_w == 0 ? "   - " : @sprintf("%5.1f", log(10, ipm.del_w)),
            ipm.alpha_z,
            ipm.alpha,
            ipm.ftype,
            ipm.cnt.l,
            get_log_relaxation(solver.rnlp),
            solver.inf_pr_cc
        )
    )
    return
end
